buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
    id "fr.coppernic.versioning" version "3.1.2"
}

apply plugin: 'com.android.library'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'checkstyle'

repositories {
    mavenCentral()
    jcenter()
    google()
}

task checkstyle(type: Checkstyle) {
    configFile rootProject.file('config/checkstyle.xml')
    source 'src/main/java'
    ignoreFailures false
    showViolations true
    include '**/*.java'

    classpath = files()
}

afterEvaluate {
    if (project.tasks.findByName('check')) {
        check.dependsOn('checkstyle')
    }
}

android {
    compileSdkVersion 26

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 26
        versionCode versioning.info.versionCode
        versionName versioning.info.display

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

String support = "26.1.0"

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$support"
    implementation 'com.jakewharton.timber:timber:4.6.0'

    compileOnly 'org.jetbrains:annotations:15.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.13.0'
    testImplementation "org.robolectric:robolectric:3.5"
    testImplementation "org.awaitility:awaitility:3.0.0"
    testImplementation "org.hamcrest:java-hamcrest:2.0.0.0"

    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}

apply from: rootProject.file('gradle/gradle-publish.gradle')
